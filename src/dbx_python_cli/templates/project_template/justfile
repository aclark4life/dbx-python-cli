# https://just.systems

default:
    echo 'Hello, world!'

[group('django')]
django-open:
    open http://localhost:8000
alias o := django-open

[group('django')]
django-serve:
    python manage.py runserver

[group('django')]
django-migrate:
    python manage.py migrate
alias m := django-migrate

[group('django')]
django-drop-mongo:
    mongosh --eval "db.getSiblingDB('{{ project_name }}').dropDatabase()"

[group('django')]
django-drop-postgres:
    psql -U postgres -c "DROP DATABASE IF EXISTS {{ project_name }}; CREATE DATABASE {{ project_name }};"

[group('django')]
django-drop: django-drop-mongo
alias d := django-drop

[group('django')]
# Create Django superuser (admin/admin)
django-create-superuser:
    export DJANGO_SUPERUSER_PASSWORD=admin && python manage.py createsuperuser --noinput --username=admin
alias su := django-create-superuser

[group('npm')]
npm-install:
    cd frontend && npm install

[group('npm')]
npm-serve:
    cd frontend && npm run watch &

[group('python')]
pip-install:
    python -m pip install --upgrade pip
    python -m pip install -e .
alias i := pip-install

serve: npm-install npm-serve django-serve

alias s := serve
